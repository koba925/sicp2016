<!DOCTYPE html>
<html>

<head>

<meta charset="utf-8">
<title>sicp13</title>



</head>

<body>

<h1 id="toc_0">SICPを読む(13)</h1>

<h2 id="toc_1">2.1.4 Extended Exercise: Interval Arithmetic</h2>

<p>もうひとつの例として区間演算を取り上げます<br>
回路の抵抗の話で説明されてますがそのへんは割愛</p>

<ul>
<li>ふたつの端点で表された「区間」という抽象化されたオブジェクトがあるものとする</li>
<li>make-interval、lower-bound、upper-boundが使えるものとする</li>
</ul>

<p>コンストラクタとセレクタが「あるものとする」やり方ですね<br>
中身はあとで考える式</p>

<div><pre><code class="language-none">(define (add-interval x y)
  (make-interval (+ (lower-bound x) (lower-bound y))
                 (+ (upper-bound x) (upper-bound y))))

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (div-interval x y)
  (mul-interval x
                (make-interval (/ 1.0 (upper-bound y))
                               (/ 1.0 (lower-bound y)))))</code></pre></div>

<p>うまくいきそうではありますがまだ動かせません</p>

<p>mul-intervalは場合分けしても書けそうですけど面倒くさそう<br>
場合分けがきちんとわかってないとテスト書けませんけど<br>
でもここはちょっとスルー</p>

<h3 id="toc_2">Exercise 2.7.</h3>

<ul>
<li>make-intervalは<code>(define (make-interval a b) (cons a b))</code>と書ける</li>
<li>upper-boundとlower-boundを定義せよ</li>
</ul>

<div><pre><code class="language-none">(define (lower-bound i) (car i))
(define (upper-bound i) (cdr i))</code></pre></div>

<h3 id="toc_3">Exercise 2.8.</h3>

<ul>
<li>区間の差を定義せよ</li>
</ul>

<div><pre><code class="language-none">(define (sub-interval x y)
  (make-interval (- (lower-bound x) (upper-bound y))
                 (- (upper-bound x) (lower-bound y))))</code></pre></div>

<h3 id="toc_4">Exercise 2.9.</h3>

<ul>
<li>区間の和（差）の誤差の幅は、加えられる２区間の誤差の幅の関数であることを示せ</li>
</ul>

<p>区間i1[a1,a2]と区間i2[b1,b2]について考えます<br>
区間の和は[a1+b1,a2+b2]ですから和の誤差の幅はw=(a2+b2)-(a1+b1)<br>
２区間の誤差の幅はそれぞれw1=a2-a1、w2=b2-b1ですから<br>
w=w1+w2となります</p>

<p>同様に区間の差は[a1-b2,a2-b1]ですからw=(a2-b1)-(a1-b2)<br>
このときもやはりw=w1+w2となります</p>

<p>プログラミングがまったく出てきてませんがいいんでしょうか</p>

<ul>
<li>区間の積（商）の誤差の幅は、加えられる２区間の誤差の幅の関数でない例を示せ</li>
</ul>

<p>例ってどういう例でしょうか<br>
[2,4]×[3,5]=[6,20]で2,2から14とか<br>
[3,8]÷[2,3]=[1,4]で5,1から3とか<br>
そういうんじゃ関数じゃない例とは言えない気がします<br>
w=w1×w2+10と書けるんです！て言われたら困ります</p>

<p>a1,a2,b1,b2がすべて正なら<br>
区間の積は[a1×b1,a2×b2]で誤差の幅はa2×b2-a1×b1<br>
これはw1とw2では書けそうにありません<br>
割り算でも同様</p>

<p>の方が説得力ありますね<br>
「例としてa1,a2,b1,b2がすべて正の場合を取り上げる」とか言えば<br>
例を挙げたって言えるのかなあ<br>
全部の場合を考えなくていいよ、っていう意味ならこれでいいのかな</p>

<h3 id="toc_5">Exercise 2.10.</h3>

<ul>
<li>0を含む区間で割った時はエラーになるようにせよ</li>
</ul>

<p>0を含むかどうかは上端と下端の積が0以下かどうかで判定できます<br>
これくらいならかっこつけすぎってことはないよね</p>

<div><pre><code class="language-none">(define (div-interval x y)
  (if (&lt;= (* (lower-bound y) (upper-bound y)) 0)
      (error &quot;Division by 0&quot;)
      (mul-interval x
                    (make-interval (/ 1.0 (upper-bound y))
                                   (/ 1.0 (lower-bound y))))))</code></pre></div>

<h3 id="toc_6">Exercise 2.11.</h3>

<ul>
<li>端点の符号で9つに場合分けしてmul-intervalを作れ</li>
<li>かけ算が2回で済まないのは9つのうちひとつだけ</li>
</ul>

<p>場合分けスルーできませんでした</p>

<p>普通なら正,0,負で分けるところを<br>
ここでは端点がふたつとも正、異符号、ふたつとも負、に分ける感じでしょうか<br>
端点が0の場合はどうするといいかな<br>
もう一方が正か負で分ける？<br>
それとも異符号の仲間に入れる？<br>
感覚的にはどっちでもいけそう<br>
異符号の仲間に入れよう<br>
あとで確かめる</p>

<p>区間の符号（と呼ぶことに）を求める関数でも作っときますか<br>
下端&lt;=上端は間違いないから両方チェックする必要はないよね</p>

<div><pre><code class="language-none">(define (plus?-interval x) (&gt; (lower-bound x) 0))
(define (minus?-interval x) (&lt; (upper-bound x) 0))</code></pre></div>

<p>愚直に場合分け</p>

<div><pre><code class="language-none">(define (mul-interval-c x y)
  (cond ((plus?-interval x)
         (cond ((plus?-interval y)  ; [1,2]x[3,4]
                (make-interval (* (lower-bound x) (lower-bound y))
                               (* (upper-bound x) (upper-bound y))))
               ((minus?-interval y) ; [1,2]x[-4,-3]
                (make-interval (* (upper-bound x) (lower-bound y))
                               (* (lower-bound x) (upper-bound y))))
               (else                ; [1,2]x[-3,4]
                (make-interval (* (upper-bound x) (lower-bound y))
                               (* (upper-bound x) (upper-bound y))))))
        ((minus?-interval x)
         (cond ((plus?-interval y)  ; [-2,-1]x[3,4]
                (make-interval (* (lower-bound x) (upper-bound y))
                               (* (upper-bound x) (lower-bound y))))
               ((minus?-interval y) ; [-2,-1]x[-4,-3]
                (make-interval (* (upper-bound x) (upper-bound y))
                               (* (lower-bound x) (lower-bound y))))
               (else                ; [-2,-1]x[-3,4]
                (make-interval (* (lower-bound x) (upper-bound y))
                               (* (lower-bound x) (lower-bound y))))))
        (else
         (cond ((plus?-interval y)  ; [-1,2]x[3,4]
                (make-interval (* (lower-bound x) (upper-bound y))
                               (* (upper-bound x) (upper-bound y))))
               ((minus?-interval y) ; [-1,2]x[-4,-3]
                (make-interval (* (upper-bound x) (lower-bound y))
                               (* (lower-bound x) (lower-bound y))))
               (else                ; [-1,2]x[-3,4]
                (mul-interval x y))))))</code></pre></div>

<p>else→elseのところは<br>
下端がa1×b2・a2×b1の小さい方、下端がa1×b1・a2×b2の大きい方、なんですが<br>
計算してみないとわからないので４回かけ算が必要になります<br>
つまりmul-intervalと同じことをやらなきゃいけないので呼び出しで書きました</p>

<p>愚直に場合分けして、式が重複してたらまとめようかと思いましたが<br>
重複してるところはありませんね<br>
全部で16とおりある組み合わせのうち8通りが登場しています<br>
なにか規則性はあるかな？</p>

<p>しかしこれ、常に正しい答えを出すという自信が持てないですね<br>
テスト書いてみました<br>
mul-intervalの中まで心配した書き方です</p>

<div><pre><code class="language-none">(check-equal? (mul-interval-c (make-interval 1 2) (make-interval 3 4))     &#39;(3 . 8))
(check-equal? (mul-interval-c (make-interval 1 2) (make-interval -4 -3))   &#39;(-8 . -3))
(check-equal? (mul-interval-c (make-interval 1 2) (make-interval -3 4))    &#39;(-6 . 8))
(check-equal? (mul-interval-c (make-interval -2 -1) (make-interval 3 4))   &#39;(-8 . -3))
(check-equal? (mul-interval-c (make-interval -2 -1) (make-interval -4 -3)) &#39;(3 . 8))
(check-equal? (mul-interval-c (make-interval -2 -1) (make-interval -3 4))  &#39;(-8 . 6))
(check-equal? (mul-interval-c (make-interval -1 2) (make-interval 3 4))    &#39;(-4 . 8))
(check-equal? (mul-interval-c (make-interval -1 2) (make-interval -4 -3))  &#39;(-8 . 4))
(check-equal? (mul-interval-c (make-interval -1 2) (make-interval -3 4))   &#39;(-6 . 8))
(check-equal? (mul-interval-c (make-interval -2 1) (make-interval -3 4))   &#39;(-8 . 6))
(check-equal? (mul-interval-c (make-interval -1 2) (make-interval -4 3))   &#39;(-8 . 6))
(check-equal? (mul-interval-c (make-interval -2 1) (make-interval -4 9))   &#39;(-18 . 9))</code></pre></div>

<p>書いたけど考慮漏れとかあるかもしれないし安心できる気がしません<br>
そうそう、端点に0を含む場合も確かめようとしてたんでした</p>

<div><pre><code class="language-none">(check-equal? (mul-interval-c (make-interval 0 2) (make-interval 3 4))     &#39;(0 . 8))
(check-equal? (mul-interval-c (make-interval -2 0) (make-interval 3 4))    &#39;(-8 . 0))
(check-equal? (mul-interval-c (make-interval 1 2) (make-interval 0 4))     &#39;(0 . 8))
(check-equal? (mul-interval-c (make-interval 1 2) (make-interval -3 0))    &#39;(-6 . 0))
(check-equal? (mul-interval-c (make-interval 0 2) (make-interval 0 4))     &#39;(0 . 8))
(check-equal? (mul-interval-c (make-interval 0 2) (make-interval -3 0))    &#39;(-6 . 0))
(check-equal? (mul-interval-c (make-interval -1 0) (make-interval 0 4))    &#39;(-4 . 0))
(check-equal? (mul-interval-c (make-interval -1 0) (make-interval -3 0))   &#39;(0 . 3))
(check-equal? (mul-interval-c (make-interval 0 0) (make-interval 3 4))     &#39;(0 . 0))
(check-equal? (mul-interval-c (make-interval 1 2) (make-interval 0 0))     &#39;(0 . 0))</code></pre></div>

<p>考えうる限りのすべての組み合わせを試したわけじゃないですがまあ大丈夫そうではあります<br>
でもお仕事とかならやってみたらうまくいってるみたいです、とか言ってちゃダメな気がします<br>
実際やることになったら普通にmul-intervalでやりたい<br>
よっぽど性能がクリティカルでかつかけ算がとても高価なときに泣く泣くいやいや書く、くらいかな</p>

<p>演習から本文に戻ります<br>
要件が変更になりました</p>

<ul>
<li>中心と幅で扱えるようにせよ</li>
</ul>

<div><pre><code class="language-none">(define (make-center-width c w)
  (make-interval (- c w) (+ c w)))
(define (center i)
  (/ (+ (lower-bound i) (upper-bound i)) 2))
(define (width i)
  (/ (- (upper-bound i) (lower-bound i)) 2))</code></pre></div>

<p>内部の表現を隠しておいたので<br>
コンストラクタとセレクタを追加しただけで同じように区間を扱えるようになりました</p>

<div><pre><code class="language-none">&gt; (add-interval (make-center-width 2 1) (make-center-width 4 2))
&#39;(3 . 9)</code></pre></div>

<p>ってことですね</p>

<h3 id="toc_7">Exercise 2.12.</h3>

<ul>
<li>誤差の幅を％で表せるようにせよ</li>
<li>パーセントを返すセレクタも作れ</li>
</ul>

<p>さっき幅で書いてたところがパーセントになっただけなので<br>
さっき作った関数を大々的に使います</p>

<div><pre><code class="language-none">(define (make-center-percent c p)
  (make-center-width c (* c p 0.01)))
(define (percent i)
  (* (/ (width i) (center i)) 100))</code></pre></div>

<h3 id="toc_8">Exercise 2.13.</h3>

<ul>
<li>誤差の％が小さければ、区間の積の誤差を近似する簡単な式があることを示せ</li>
<li>数はすべてせいであると仮定してよい</li>
</ul>

<div><pre><code class="language-none">  [c1-c1p1,c1+c1p1]×[c2-c2p2,c2+c2p2]
= [(c1-c1p1)×(c2-c2p2),(c1+c1p1)×(c2+c2p2)] ;数がすべて正だから
= [c1c2-c1c2p2-c2c1p1+c1p1c2p2,c1c2+c1c2p2+c2c1p1+c1p1c2p2]
= [c1c2(1-(p1+p2)+c1c2p1p2),c1c2(1+p1p2)+c1c2p1p2]</code></pre></div>

<p>誤差のwidthは</p>

<div><pre><code class="language-none">  ((c1c2(1+(p1+p2)+c1c2p1p2))-(c1c2(1-p1p2)+c1c2p1p2))/2
= c1c2(p1+p2)</code></pre></div>

<p>あれーここでp1,p2は小さいからp1p2はもっと小さくて無視できる、ってやるつもりだったのに<br>
p1p2が消えちゃった<br>
大丈夫かな</p>

<p>誤差のパーセントは、えっとcenterで割ればいいんだな<br>
centerは</p>

<div><pre><code class="language-none">  ((c1c2(1-(p1+p2)+c1c2p1p2))+(c1c2(1+p1p2)+c1c2p1p2))/2
= c1c2+c1c2p1p2
= c1c2(1+p1p2)
≒ c1c2</code></pre></div>

<p>こっちで出てきました<br>
というわけで誤差のパーセントはだいたい</p>

<div><pre><code class="language-none">  c1c2(p1+p2)/c1c2
= p1+p2</code></pre></div>

<p>どれどれ</p>

<div><pre><code class="language-none">&gt; (mul-interval (make-center-percent 100 1) (make-center-percent 200 2))
&#39;(19404.0 . 20604.0)
&gt; (center (mul-interval (make-center-percent 100 1) (make-center-percent 200 2)))
20004.0
&gt; (percent (mul-interval (make-center-percent 100 1) (make-center-percent 200 2)))
2.9994001199760048</code></pre></div>

<p>だいたいあってる</p>

<p>このへんは工学の人向けでしょうか<br>
また本文に戻ります</p>

<p>この関数たちを使って以下のふたつの式を計算します<br>
変形しただけで同じ式です<br>
抵抗R1、R2を並列に接続した時の抵抗値を表しています</p>

<div><pre><code class="language-none">R1R2/(R1+R2)
1/(1/R1)+(1/R2)</code></pre></div>

<p>ところが計算の結果が違うと</p>

<p>Schemeで書くとこんな関数</p>

<div><pre><code class="language-none">(define (par1 r1 r2)
  (div-interval (mul-interval r1 r2)
                (add-interval r1 r2)))
(define (par2 r1 r2)
  (let ((one (make-interval 1 1)))
    (div-interval one
                  (add-interval (div-interval one r1)
                                (div-interval one r2)))))</code></pre></div>

<p>計算してみます</p>

<div><pre><code class="language-none">&gt; (define R1 (make-center-percent 100 10))
&gt; (define R2 (make-center-percent 200 10))
&gt; (center (par1 R1 R2))
69.36026936026937
&gt; (percent (par1 R1 R2))
29.223300970873783
&gt; (center (par2 R1 R2))
66.66666666666667
&gt; (percent (par2 R1 R2))
10.000000000000005</code></pre></div>

<p>けっこう違いますね<br>
誤差が30％にもなったり<br>
centerまでずれるのはちょっと意外でした</p>

<p>さっきの演習でやりましたが<br>
かけ算のときは誤差の幅がかけられる数の誤差の和になるのが効いてそう</p>

<p>誤差がなければ66.666...になります<br>
ふたつ目の式のほうが明らかに性能がいいですね<br>
oneには誤差がないので、oneとの演算では誤差が大きくならないってことかな</p>

<p>中途半端なところですが今回はここまで</p>




</body>

</html>
