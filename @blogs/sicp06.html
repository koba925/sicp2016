<!DOCTYPE html>
<html>

<head>

<meta charset="utf-8">
<title>sicp06</title>



</head>

<body>

<h1 id="toc_0">SICPを読む(6)</h1>

<h3 id="toc_1">Exercise 1.23.</h3>

<ul>
<li>smallest-divisorは2,3,4,5,...で割っていてもったいない</li>
<li>(next test-divisor)を定義して2,3,5,7,...で割るようにせよ</li>
<li>時間は半分になったか？</li>
</ul>

<p>find-divisorを書き換えます</p>

<div><pre><code class="language-none">(define (next test-divisor)
  (if (= test-divisor 2)
      3
      (+ test-divisor 2)))
(define (find-divisor n test-divisor)
  (cond ((&gt; (square test-divisor) n) n)
        ((divides? n test-divisor) test-divisor)
        (else (find-divisor n (next test-divisor)))))</code></pre></div>

<p>どれどれ</p>

<div><pre><code class="language-none">&gt; (search-for-primes 1001 3)
0.0030110677083333335
&gt; (search-for-primes 10001 3)
0.007405598958333333
&gt; (search-for-primes 100001 3)
0.019694010416666668
&gt; (search-for-primes 1000001 3)
0.06005859375</code></pre></div>

<p>1001 0.00390 → 0.00301 1.29倍<br>
10001 0.00936 → 0.00741 1.26倍<br>
100001 0.0297 → 0.0197 1.50倍<br>
1000001 0.0937 → 0.0601 1.55倍</p>

<p>というわけで2倍速くはなりませんでした<br>
考えられるのはこれくらい？</p>

<p>a) (+1 test-divisor)が(next test-divisor)になって遅くなった分<br>
b) start-timeを取得してからprime?を呼び出すまでにかかった時間<br>
c) prime?が終了してからfinished-timeを取得するまでの時間<br>
d) 測定条件というかたまたま(何度もやってみると少しばらつきがある)</p>

<p>補正できるかな？</p>

<p>a) は(+1 test-divisor)の代わりにこういうのを使えばほぼフェアな比較になるはず</p>

<div><pre><code class="language-none">(define (next test-divisor)
  (if (= test-divisor 2)
      3
      (+ test-divisor 1)))</code></pre></div>

<div><pre><code class="language-none">&gt; (search-for-primes 1001 3)
0.004313151041666667
&gt; (search-for-primes 10001 3)
0.012288411458333334
&gt; (search-for-primes 100001 3)
0.037272135416666664
&gt; (search-for-primes 1000001 3)
0.11832682291666667</code></pre></div>

<p>これを使って比較する</p>

<p>b)、c)はprime?を呼びださないで時間を測定してみればいいかな</p>

<div><pre><code class="language-none">    (if (prime? n)
        (elapsed-time (current-inexact-milliseconds))
        #f))</code></pre></div>

<p>を単に</p>

<div><pre><code class="language-none">    (elapsed-time (current-inexact-milliseconds))</code></pre></div>

<p>にして</p>

<div><pre><code class="language-none">&gt; (timed-prime? 2)
0.0009765625</code></pre></div>

<p>1ミリ秒くらいはかかるんだな<br>
これは思ったより大きい<br>
defineの中にdefineを入れると毎回defineを実行して時間がかかる？</p>

<p>さてどうなった</p>

<div><pre><code class="language-none">&gt; (/ (- 0.004313151041666667 0.0009765625) (- 0.0030110677083333335 0.0009765625))
1.6400000000000001
&gt; (/ (- 0.012288411458333334 0.0009765625) (- 0.007405598958333333 0.0009765625))
1.7594936708860762
&gt; (/ (- 0.037272135416666664 0.0009765625) (- 0.019694010416666668 0.0009765625))
1.9391304347826084
1.9862258953168044
&gt; (/ (- 0.11832682291666667 0.0009765625) (- 0.06005859375 0.0009765625))
1.9862258953168044</code></pre></div>

<p>数が大きい時はかなりいい感じになったけど数が少ない時がまだ2倍にならないな<br>
まだ数によらない部分で時間がかかっているところがあるということ？<br>
補正したけど補正しきれてないだけ？</p>

<p>こういうとこついコピペでやっちゃうのは怠惰の美徳が足りてないっぽい気がする<br>
全体的に書き直し</p>

<div><pre><code class="language-none">(define (prime? next)
  (lambda (n) (= n (smallest-divisor n next))))

(define (smallest-divisor n next)
  (find-divisor n 2 next))

(define (find-divisor n test-divisor next)
  (cond ((&gt; (square test-divisor) n) n)
        ((divides? n test-divisor) test-divisor)
        (else (find-divisor n (next test-divisor) next))))

(define (slow-next test-divisor)
  (if (= test-divisor 2)
      3
      (+ test-divisor 1)))

(define (fast-next test-divisor)
  (if (= test-divisor 2)
      3
      (+ test-divisor 2)))

(define (square x) (* x x))

(define (divides? a b)
  (= (remainder a b) 0))

(define (timed-prime? test? n)
  (let ((start-time (current-inexact-milliseconds)))
    (if (test? n)
        (- (current-inexact-milliseconds) start-time)
        #f)))

(define (search-for-primes test? lower count)
  (define (iter n found total-time)
    (define (next elapsed-time)
      (cond (elapsed-time
             ;(printf &quot;~a : ~a~n&quot; n elapsed-time)
             (iter (+ n 2) (+ found 1) (+ total-time elapsed-time)))
            (else
             (iter (+ n 2) found total-time))))
    (if (= found count)
        total-time
        (next (timed-prime? test? n))))
  (/ (iter lower 0 0) count))

; オーバーヘッド測定用の何もしないテスト
(define (dummy n) #t)

(define (average-time times test? lower count)
  (define (iter c total-time)
    (if (= c times)
        total-time
        (iter (+ c 1) (+ total-time
                         (search-for-primes test? lower count)))))
  (/ (iter 0 0) times))

(define (slow-fast-ratio n)
  
  ;(printf &quot;~nn=~a~n&quot; n)
  (let ((d (average-time 10 dummy n 3))
        (s (average-time 10 (prime? slow-next) n 3))
        (f (average-time 10 (prime? fast-next) n 3)))
    ;(printf &quot;d=~a~n&quot; d)
    ;(printf &quot;s=~a~n&quot; s)
    ;(printf &quot;f=~a~n&quot; f)
    ;(printf &quot;s/f=~a~n&quot; (/ s f))
    ;(printf &quot;(s-d)/(f-d)=~a~n&quot; (/ (- s d) (- f d)))
    (/ (- s d) (- f d))))

(define (average-ratio times n)
  (define (iter c total-time)
    (if (= c times)
        total-time
        (iter (+ c 1) (+ total-time (slow-fast-ratio n)))))
  (/ (iter 0 0) times))

(define (ex1-23)

  ; 1回目の実行に時間がかかるため一度実行しておく
  (search-for-primes dummy 2 1)
  (search-for-primes (prime? slow-next) 2 1)
  (search-for-primes (prime? fast-next) 2 1)

  (printf &quot;   1001 : ~a~n&quot; (average-ratio 10 1001))
  (printf &quot;  10001 : ~a~n&quot; (average-ratio 10 10001))
  (printf &quot; 100001 : ~a~n&quot; (average-ratio 10 100001))
  (printf &quot;1000001 : ~a~n&quot; (average-ratio 10 1000001)))

(ex1-23)</code></pre></div>

<p>フライングですがlambdaとかletとか使ってます</p>

<p>この形にたどり着くまでにいろいろと紆余曲折が</p>

<p>まず1度目の関数実行だけ時間がかかることに気づきました<br>
何でかな<br>
測定のためにはこれはどけておかないといけないので<br>
とりあえず1回実行してから測定するようにしました<br>
さっき何もしないときの時間がけっこうかかると思ったのはこれかもしれない<br>
あと数が小さい時の結果が思わしくなかったのも</p>

<p>しかしなかなかばらつきが大きい<br>
実行するたびに1.5倍〜2.2倍くらいの間でまったく異なる結果が出てしまいます</p>

<p>試行が1回づつだからかなと思い<br>
dummyを10回実行した平均、<br>
(prime? slow-next)を10回実行した平均、<br>
(prime? slow-next)を10回実行した平均<br>
を取ってから計算しましたがまだばらつきが大きい</p>

<p>100回にしても1000回にしてもまだばらつきが大きい<br>
しかも、数十回くらい実行すると1回の試行にかかる時間が急に短くなることがある<br>
実行時に最適化しているのかキャッシュにでも入ったのか<br>
これはなかなか手ごわい</p>

<p>半ばヤケ気味に上で出した平均で遅い方と速い方の比を計算し<br>
それを10回繰り返して平均を取ったらやっとそこそこ安定しました</p>

<div><pre><code class="language-none">   1001 : 1.8337048475130946
  10001 : 1.9836974335283653
 100001 : 2.0142126400853986
1000001 : 1.9625607023080118</code></pre></div>

<p>1001の結果が微妙にアレです<br>
統計の人ならこりゃ有意に差がありますねとか言いそうです</p>

<p>数によらない要因がまだ残ってるかなあ<br>
prime?の代わりにdummyを使ってオーバーヘッドを計測するのはまだ完全じゃないか<br>
timed-prime?でstart-timeを取得→prime?呼び出し→smallest-divisor呼び出し<br>
あたりが含まれてないことになるし<br>
find-divisorをダミーに置き換えるほうが正確になるかな<br>
こう？</p>

<div><pre><code class="language-none">(define (dummy-prime? next)
  (lambda (n) (= n (dummy-smallest-divisor n next))))

(define (dummy-smallest-divisor n next)
  (dummy-find-divisor n 2 next))

(define (dummy-find-divisor n test-divisor next)
  n)</code></pre></div>

<p>どれ</p>

<div><pre><code class="language-none">   1001 : 1.8200530290372314
  10001 : 2.0692433092477485
 100001 : 1.988264206864656
1000001 : 1.9388971246922337</code></pre></div>

<p>あんまかわらんすよ<br>
これくらいで手を打ちませんか先生</p>

<p>ばらつきに一定の傾向がみられることも悩ましさに拍車をかけてました<br>
ただバラバラなら平均をとって終わりにすることもできると思うんですが<br>
すこし工夫すれば取り除けそうで<br>
こういう場合は中央値を取った方がいいのかも、とか<br>
それとも上下いくつかを外してから平均を取ろうか、とか</p>

<h3 id="toc_2">Exercise 1.24.</h3>

<ul>
<li>fast-prime?でさっき見つけた12個の素数を試してみなさい</li>
<li>1000000にかかる時間は1000にかかる時間にくらべてどうか？</li>
</ul>

<p>Θ(log n)なので2倍で済むはず</p>

<div><pre><code class="language-none">(define (ex1-24)
  ;例によって一度やっておく
  (timed-prime? (lambda (n) (fast-prime? n 1)) 2)
  
  (reported-timed-prime? 1009)
  (reported-timed-prime? 1013)
  (reported-timed-prime? 1019)
  (reported-timed-prime? 10007)
  (reported-timed-prime? 10009)
  (reported-timed-prime? 10037)
  (reported-timed-prime? 100003)
  (reported-timed-prime? 100019)
  (reported-timed-prime? 100043)
  (reported-timed-prime? 1000003)
  (reported-timed-prime? 1000033)
  (reported-timed-prime? 1000037))

(define (reported-timed-prime? n)
  (printf &quot;~a : ~a~n&quot;
          n
          (timed-prime? (lambda (n) (fast-prime? n 100)) n)))

(define (timed-prime? test? n)
  (let ((start-time (current-inexact-milliseconds)))
    (if (test? n)
        (- (current-inexact-milliseconds) start-time)
        #f)))</code></pre></div>

<p>とりあえず素直に何も補正せず1回きりの試行で<br>
fast-prime?で何回試すのが相場なのかよくわかりませんが100回で</p>

<div><pre><code class="language-none">1009 : 0.447021484375
1013 : 0.257080078125
1019 : 0.377197265625
10007 : 0.491943359375
10009 : 0.364990234375
10037 : 0.43212890625
100003 : 0.572998046875
100019 : 0.489013671875
100043 : 0.43505859375
1000003 : 0.385009765625
1000033 : 0.38916015625
1000037 : 0.39111328125</code></pre></div>

<p>(つд⊂)ｺﾞｼｺﾞｼ<br>
1000000の方が速くなってます！<br>
どうすんすか先生<br>
これはあれかな<br>
繰り返しやってると速くなる現象かな<br>
なんかそういうのなくさないと</p>

<p>順番を逆にしてみた</p>

<div><pre><code class="language-none">1000003 : 0.431884765625
1000033 : 0.4521484375
1000037 : 0.359130859375
100003 : 0.2978515625
100019 : 0.308837890625
100043 : 0.31201171875
10007 : 0.263916015625
10009 : 0.2509765625
10037 : 0.260986328125
1009 : 0.242919921875
1013 : 0.2080078125
1019 : 0.215087890625</code></pre></div>

<p>お、なんか理論値に近づいた気が<br>
よし先生にはこの値を提出しよう（捏造じゃないよ</p>

<p>さておき<br>
ちゃんと測定できてる気になれないと先へ進めません<br>
困った</p>

<p>また平均を取るか・・・<br>
ってこれ100回やった平均みたいなもんだろ<br>
どうすんだよ</p>

<p>1000000回やらせてみた（力技</p>

<div><pre><code class="language-none">1009 : 1959.90185546875
1013 : 2031.31396484375
1019 : 2123.968994140625
10007 : 2607.056884765625
10009 : 2477.416015625
10037 : 2539.02392578125
100003 : 2922.492919921875
100019 : 3010.7470703125
100043 : 3047.215087890625
1000003 : 3417.47705078125
1000033 : 3478.629150390625
1000037 : 3495.89306640625</code></pre></div>

<p>まあまあ揃ってきた？</p>

<div><pre><code class="language-none">10007 : 2607.056884765625
10009 : 2477.416015625</code></pre></div>

<p>こういうのはもう諦めよう<br>
再起動直後にこれだけ起動するとかすればもうちょっとくらいは揃うのかもしれないけど<br>
いや、再起動直後じゃかえってばらつくかも？</p>

<p>さてΘ(log n)さん的には2倍時間がかからないとおかしいわけですが<br>
同じ関数でnが変わっているだけなので1.23よりも条件が揃ってて<br>
nに影響されない分を引いてやるくらいしか思いつかないです</p>

<p>どこまでがnに影響されない部分かな<br>
expmodの外側は全部ってことになるか<br>
expmodをダミー化</p>

<div><pre><code class="language-none">(define (ex1-24-2)
  (let ((d (timed-prime? (lambda (n) (dummy-fast-prime? n 1000000)) 1009))
        (t1 (timed-prime? (lambda (n) (fast-prime? n 1000000)) 1009))
        (t2 (timed-prime? (lambda (n) (fast-prime? n 1000000)) 1000003)))
    (printf &quot;d     = ~a~n&quot; d)
    (printf &quot;t1    = ~a~n&quot; t1)
    (printf &quot;t2    = ~a~n&quot; t2)
    (printf &quot;t2/t1 = ~a~n&quot; (/ t2 t1))
    (printf &quot;(t2-d)/(t1-d) = ~a~n&quot; (/ (- t2 d) (- t1 d)))))

(define (dummy-fast-prime? n times)
    (cond ((= times 0) true)
          ((dummy-fermat-test n) (dummy-fast-prime? n (- times 1)))
          (else false)))

(define (dummy-fermat-test n)
  (define (try-it a)
    (= (dummy-expmod a n n) a))
  (try-it (+ 1 (random (- n 1)))))

(define (dummy-expmod base exp m) base)</code></pre></div>

<p>さて</p>

<div><pre><code class="language-none">d     = 271.924072265625
t1    = 1926.79296875
t2    = 3389.715087890625
t2/t1 = 1.7592523653901906
(t2-d)/(t1-d) = 1.88401088584629</code></pre></div>

<p>説明がついたとは言い切れない比<br>
しかしギブアップ</p>




</body>

</html>
